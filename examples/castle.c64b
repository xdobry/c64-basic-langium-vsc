1 GOTO 5
2 REM
3 REM ZEILEN 0 UND 1 NICHT AENDERN !!!!
4 REM
5 DIM V$(14),H$(21),RI$(19),ZI$(19),G$(12),G (12)
6 L$="{rvrs on}                        "
7 CL$="{$a5}                      {$a7}"
8 EF$="                                       "
10 POKE 53280,14 :POKE 53281,14 :POKE 53272,23 :P=15 :QQ=54272 :VI=53248
15 POKE VI+21,0 :PRINT "{clear}" SPC(12) "{black}{down:8}CASTLE{shft space}OF{shft space}DOOM" :REM CASTLE OF DOOM
20 PRINT "{down:2}{white}    EIN ADVENTURE VON B.WEISSBECKER"
25 PRINT SPC(11) "{down:2}* BITTE WARTEN *"
30 FOR I=1 TO 14 :READ V$(I)
35 IF I<10 THEN READ KB$(I)
40 NEXT
45 FOR I=1 TO 21 :READ H$(I) :NEXT
50 FOR I=1 TO 19 :READ RI$(I),ZI$(I) :NEXT
55 FOR I=1 TO 12 :G$(I)=H$(I) :READ G (I) :NEXT
60 FOR I=1 TO 4 :READ ZX$(I),ZY$(I) :NEXT
65 FOR I=13 TO 15
70 FOR J=0 TO 62 :READ K :POKE 64*I+J,K :NEXT :NEXT
85 PRINT "{up}  MOECHTEN SIE EINE ERKLAERUNG ? (J/N)"
90 GET A$:IF A$="J" THEN 7500
95 IF A$<>"N" THEN 90
100 PRINT "{clear}{black}" SPC(10) "{down}CASTLE{shft space}OF{shft space}DOOM"
105 PRINT "{down:2}{cyan} BEI DIESEM ADVENTURE KOENNEN SIE ZWI-"
110 PRINT "{down} SCHEN DREI VERSIONEN WAEHLEN :" :PRINT "{down}  WOLLEN SIE :"
115 PRINT "{down}  1 - EINE VERZAUBERTE PRINZESSIN" :PRINT SPC(6) "{down}RETTEN"
120 PRINT "{down}  2 - EINEN BOESEN ZAUBERER BESIEGEN"
125 PRINT "{down}  3 - EINEN SCHATZ SUCHEN"
130 PRINT "{down:2} BITTE GEBEN SIE DIE ENTSPRECHENDE"
135 PRINT "{down} KENNZIFFER EIN."
140 POKE 198,0 :WAIT 198,1
145 GET I$:IF ASC(I$)<49 OR ASC(I$)>51 THEN 140
150 V=VAL(I$) :IF V<>2 THEN 160
155 RI$(13)="W-O" :ZI$(13)="1214" :RI$(9)="W-O-R" :ZI$(9)="081018"
157 RI$(8)="N-S-O" :ZI$(8)="051209"
160 IF V=1 THEN G (7)=1
165 FOR I=1 TO 4 :J (I)=1+4*RND(TI) :NEXT
170 Z1$=ZX$(J (1))+ZY$(J (2))
175 Z2$=ZX$(J (3))+ZY$(J (4))
176 IF Z1$=Z2$THEN 165
177 TI$="000000"
179 REM **** BILDER ZEICHNEN ****
180 PRINT "{clear}{black}";:POKE 53272,21 :POKE VI+21,0 :POKE VI+23,0 :POKE VI+29,0
185 FOR I=1 TO 20 :PRINT SPC(8) CL$:NEXT
190 FOR I=1824 TO 1863 :POKE I,99 :POKE I+QQ,0 :NEXT
195 IF P>11 THEN 415
197 POKE 53280,5 :POKE 53281,5
200 PRINT "{home}";:FOR I=1 TO 4 :PRINT SPC(8+I) "M" SPC(22-2*I) "N" :NEXT
205 PRINT SPC(13) "{up}{$af:14}"
210 FOR I=1 TO 10 :PRINT SPC(12) "{$a7}" SPC(14) "{$a5}" :NEXT
215 PRINT SPC(13) "{$b7:14}{up}"
220 FOR I=1 TO 5 :PRINT SPC(13-I) "N" SPC(12+2*I) "M" :NEXT
225 FOR I=1 TO LEN(RI$(P)) STEP 2 :A$=MID$(RI$(P),I,1)
230 IF A$="N" THEN 250
235 IF A$="W" THEN 260
240 IF A$="O" THEN 265
245 NEXT :GOTO 270
250 FOR J=1402 TO 1562 STEP 40 :FOR K=0 TO 3 :POKE J+K,160 :POKE J+K+QQ,0 :NEXT :NEXT
255 POKE 1402,254 :POKE 1405,252 :GOTO 245
260 FOR J=1552 TO 1752 STEP 40 :POKE J,160 :POKE QQ+J,0 :NEXT :POKE 1752,105 :GOTO 245
265 FOR J=1575 TO 1775 STEP 40 :POKE J,160 :POKE QQ+J,0 :NEXT :POKE 1775,95 :GOTO 245
270 ON P GOTO 275,740,300,315,740,325,345,740,355,375,395
275 ON V GOTO 740,280,285
280 POKE 2040,15 :POKE VI+39,0 :POKE VI,150 :POKE VI+1,140 :POKE VI+29,1 :POKE VI+23,1
282 POKE VI+21,1 :ZB=1 :GOTO 740
285 PRINT "{home}{down:10}" SPC(13) "{$af:4}" :PRINT SPC(12) "N   N{$b4}"
290 PRINT SPC(11) "N   N {$b4}" :PRINT SPC(11) "O{$b7:2}P  {$b4}" :PRINT SPC(11) "{$b4}$${$aa} N"
295 PRINT SPC(11) "L{$af:2}{$ba}N SCHATZ" :FOR I=1 TO 2000 :NEXT :GOTO 7830
300 IF LEFT$(RI$(3),1)="N" THEN 740
305 FOR J=1402 TO 1562 STEP 40 :FOR K=0 TO 3
310 POKE J+K,160 :POKE J+K+QQ,06 :NEXT :NEXT :PRINT SPC(16) "{up:4}{black}SPIEGEL" :GOTO 740
315 PRINT "{home}{down:2}" SPC(13) "{$df}{rvrs on}    {rvrs off}{shft pound}" :PRINT SPC(14) "{$df}{rvrs on}  {rvrs off}{shft pound}" :PRINT SPC(15) "{$b4}{$aa}"
320 FOR I=1 TO 10 :PRINT SPC(15) "OP" :NEXT :GOTO 740
325 PRINT "{home}{brown}{down:10}" SPC(13) "{$af:5}" :PRINT SPC(13) "L{$af:3}{$ba}"
330 PRINT SPC(12) "N    N{$b4}" :PRINT SPC(11) "N    NN{$b4}" :PRINT SPC(10) "N    NN{$b7}"
335 PRINT SPC(9) "O{$b7:3}PNN" :PRINT SPC(9) "{$b4}   {$aa}N" :PRINT SPC(9) "L{$af:3}{$ba}  SCHLAFZIMMER"
340 GOTO 740
345 IF GE=1 THEN 740
350 POKE 2040,13 :POKE VI+39,1 :POKE VI,150 :POKE VI+1,150 :POKE VI+23,1 :POKE VI+29,1
351 POKE VI+21,1 :IF G (12)<>0 THEN FOR I=1 TO 2000 :NEXT :GOTO 7630
352 GS=1 :GOTO 740
355 PRINT "{home}{black}{down}" SPC(11) "BURGHOF / BRUNNEN" :PRINT SPC(13) "{down}OLOLOLOP{$ba}P{$ba}P{$ba}P"
360 PRINT SPC(17) "{down:9}{$a4}{$af}{$b9:2}{$af}{$a4}" :PRINT SPC(16) "{rvrs on}        "
365 PRINT SPC(16) "{$b4}{$a3}{$b7}{$b8:2}{$b7}{$a3}{$aa}" :PRINT SPC(16) "{$b4}      {$aa}" :PRINT SPC(16) "{$b7}EDCCDE{$b7}"
370 GOTO 740
375 POKE 214,14 :PRINT
380 IF FA=1 THEN PRINT SPC(23) "{down}{$df}{rvrs on}   {$df}" :PRINT SPC(24) "{$df}{rvrs on}   {$df}" :GOTO 740
385 PRINT SPC(23) "{$af:4}" :PRINT SPC(23) "M   M"
390 PRINT SPC(16) "QUADRAT M   M" :PRINT SPC(25) "{$b7:4}" :GOTO 740
395 PRINT "{home}{down:9}" SPC(23) "{$af:4}" :PRINT SPC(22) "{$aa}M   M" :PRINT SPC(22) "{$aa} OP{$b7}P"
400 PRINT SPC(15) "L{$ba}     {$aa} {$b4}{$aa}.{$aa}" :PRINT SPC(15) "L{$ba}     {$aa} {$b4}{$aa} {$aa}"
405 PRINT SPC(14) "{rvrs on}{shft pound}  {$df}{rvrs off}{$b7:5}ML{$ba}{$af}{$ba}" :PRINT SPC(13) "{rvrs on}{shft pound}    {$df}"
410 PRINT SPC(21) "SCHRANK" :GOTO 740
415 IF P>14 THEN 445
417 POKE 53280,8 :POKE 53281,8
420 PRINT "{home}";:FOR I=1 TO 6 :PRINT SPC(8) "{blue}" L$:NEXT
425 FOR I=55504 TO 55514 STEP 2 :POKE I,7 :NEXT :FOR I=55517 TO 55527 STEP 2 :POKE I,7 :NEXT
430 FOR I=1 TO 11 :PRINT "{yellow}" SPC(8) L$:NEXT :FOR I=1 TO 3 :PRINT "{brown}" SPC(8) L$:NEXT
435 IF LEFT$(RI$(P),1)<>"N" THEN 740
440 POKE 214,11 :PRINT "{black}" :FOR I=1 TO 5 :PRINT SPC(18) "{rvrs on}    " :NEXT
442 GOTO 740
445 POKE 53280,14 :POKE 53281,14 :ON P-14 GOTO 555,450,485,500,540
450 PRINT "{home}{yellow}{down:6}" SPC(20) "{$ac}" :PRINT SPC(18) "{rvrs on}   "
460 PRINT SPC(17) "{brown}{$ac}{rvrs on} N {$bc}" :PRINT SPC(16) "{rvrs on}{shft pound} N   {$bc}"
465 PRINT SPC(15) "{$ac}{rvrs on} (      {$bc}" :PRINT SPC(14) "{$a2}{rvrs on}{$be}  M       {$bc}"
470 PRINT SPC(13) "{rvrs on}{shft pound}     M       {$bc}"
475 FOR I=1 TO 7 :PRINT SPC(8) L$:NEXT
480 PRINT SPC(9) "{up:6}{rvrs on}BERG"
481 IF LEN(RI$(16))=5 THEN PRINT SPC(9) "{down}{rvrs on}STEILER WEG NACH OBEN"
482 GOTO 740
485 PRINT "{home}" SPC(9) "{down:4}STEILER BERGHANG," :PRINT SPC(9) "{down}HOEHLE"
490 PRINT SPC(21) "{down:3}{$a2}" :PRINT SPC(19) "{$ac}{rvrs on}   {$be}{$bc}{rvrs off}{$bb}"
495 FOR I=1 TO 6 :PRINT SPC(19) "{rvrs on}       " :NEXT :GOTO 740
500 PRINT "{home}";:FOR I=1 TO 19 :PRINT SPC(8) L$:NEXT
505 PRINT "{home}" SPC(9) "{rvrs on}{down}IM BRUNNEN ..."
510 IF V<>3 THEN 740
515 IF MO=1 THEN PRINT "{down}" :GOTO 530
520 POKE 2040,14 :POKE VI+39,13 :POKE VI,200 :POKE VI+1,150 :POKE VI+23,1 :POKE VI+29,1
525 PRINT SPC(9) "{rvrs on}{down}MONSTER" :POKE VI+21,1 :MR=1
530 PRINT SPC(9) "{rvrs on}{down}TUER{down:5}" :FOR I=1 TO 7 :PRINT SPC(16) "     " :NEXT
535 IF RI$(18)="H" THEN POKE 1601,114
537 GOTO 740
540 PRINT "{home}";:FOR I=1 TO 20 :PRINT SPC(8) L$:NEXT
545 PRINT "{home}" SPC(9) "{rvrs on}{down}IN DER HOEHLE ..."
550 PRINT SPC(9) "{rvrs on}{down}STOCKDUNKEL HIER ..." :GOTO 740
555 PRINT "{home}{red}{down}" SPC(13) "O{$b7:11}P" :PRINT SPC(13) "{$b4} B A S A R {$aa}"
560 PRINT SPC(13) "L{$af:11}{$ba}"
565 IF XX=1 THEN 655
570 PRINT SPC(9) "{black}{down}EIN   BEFREUNDETER"
575 PRINT SPC(9) "{down}HAENDLER IST BEREIT," :PRINT SPC(9) "{down}IHNEN ZWEI DER FOLGEN-"
580 PRINT SPC(9) "{down}DEN GEGENSTAENDE ZU" :PRINT SPC(9) "{down}UEBERLASSEN :"
585 PRINT SPC(9) "{down}{rvrs on}1{rvrs off} GLASKUGEL {rvrs on}2{rvrs off} KAEFIG"
590 PRINT SPC(9) "{down}{rvrs on}3{rvrs off} AMULETT   {rvrs on}4{rvrs off} BUCH"
595 PRINT "{down:4}  BITTE ENTSPRECHENDE NUMMERN EINGEBEN"
600 GET I$:IF I$="" THEN 600
605 IF ASC(I$)<49 OR ASC(I$)>52 THEN 600
610 ON VAL(I$) GOSUB 635,640,645,650
615 GET J$:IF J$="" OR J$=I$THEN 615
620 IF ASC(J$)<49 OR ASC(J$)>52 THEN 615
625 ON VAL(J$) GOSUB 635,640,645,650
630 XX=1 :GG=GG+2 :GOSUB 7000 :GOTO 655
635 POKE 1633,49 :G (1)=0 :RETURN
640 POKE 1645,50 :G (2)=0 :RETURN
645 POKE 1713,51 :G (3)=0 :RETURN
650 POKE 1725,52 :G (4)=0 :RETURN
655 YY=0 :FOR I=1 TO 4 :IF G (I)=0 THEN YY=YY+1
657 NEXT :IF YY<>2 THEN 710
660 PRINT "{home}{black}{down:5}" SPC(9) "WOLLEN SIE DIE GEGEN-"
665 PRINT SPC(9) "{down}STAENDE, DIE SIE HIER" :PRINT SPC(9) "{down}ERHALTEN HABEN, NOCH  "
670 PRINT SPC(9) "{down}EINMAL UMTAUSCHEN ?" :PRINT SPC(9) "{down}        (J/N)"
675 PRINT :PRINT SPC(8) CL$:PRINT :PRINT SPC(8) CL$:GOSUB 860
680 GET I$:IF I$="N" THEN 710
685 IF I$<>"J" THEN 680
690 FOR I=1 TO 4 :G (I)=20 :NEXT :GG=GG-2
695 PRINT "{home}{down:4}" :FOR I=1 TO 9 :PRINT SPC(8) CL$:NEXT
700 PRINT "{home}{down:5}" SPC(12) "WAEHLEN SIE !" :PRINT "{down:7}"
705 GOTO 585
710 PRINT "{home}{down:4}{black}" :FOR I=1 TO 9 :PRINT SPC(8) CL$:NEXT
715 PRINT "{home}{down:3}" :FOR I=1 TO 5 :PRINT SPC(13) "{$b5}" SPC(11) "{$b6}" :NEXT
720 PRINT SPC(13) "{cyan}O{$b7:11}P" :PRINT SPC(13) "{$b4}O{$b7}P O{$b7}P O{$b7}P{$aa}"
725 PRINT SPC(13) "{$b4:2}{red}Q{cyan}{$aa} {$b4}{red}Q{cyan}{$aa} {$b4}{red}Q{cyan}{$aa:2}"
730 PRINT SPC(13) "{$b4}L{$af}{$ba} L{$af}{$ba} L{$af}{$ba}{$aa}" :PRINT SPC(13) "L{$af:11}{$ba}"
740 POKE 214,18 :PRINT :PRINT SPC(9) "{black}RICHTUNGEN : {white}" RI$(P) :GOTO 750
748 REM **** KOMMANDOAUSWERTUNG ****
749 FOR I=1 TO 2500 :NEXT
750 ZA=0 :GOSUB 7000
755 GOSUB 860 :INPUT "{yellow} KOMMANDO :";K$
760 IF LEFT$(K$,1)=" " THEN K$=RIGHT$(K$,LEN(K$)-1) :GOTO 760
765 IF LEN(K$)=1 THEN 870
770 FOR I=1 TO LEN(K$) :IF MID$(K$,I,1)=" " THEN 780
772 NEXT
775 GOSUB 860 :PRINT "          UNKORREKTE EINGABE !" :GOTO 749
780 V$=LEFT$(K$,I-1) :H$=RIGHT$(K$,LEN(K$)-I)
785 IF LEFT$(H$,1)=" " THEN H$=RIGHT$(H$,LEN(H$)-1) :GOTO 785
790 IF V$="SAGE" THEN 6500
795 FOR I=1 TO 14 :IF LEFT$(V$(I),LEN(V$))=V$THEN X=I :GOTO 805
800 NEXT :NV$=V$:GOTO 815
805 FOR I=1 TO 21 :IF LEFT$(H$(I),LEN(H$))=H$THEN Y=I :GOTO 825
810 NEXT :NV$=H$
815 GOSUB 860 :PRINT "{up} TUT MIR LEID, ABER ICH VERSTEHE"
820 PRINT "{down} '" NV$"' NICHT." :GOTO 749
825 IF X>9 THEN X=X-5
830 FOR I=1 TO LEN(KB$(X)) :IF MID$(KB$(X),I,1)=CHR$(Y+64) THEN 950
835 NEXT :GOSUB 860
840 PRINT "{up} TUT MIR LEID, ABER ICH VERSTEHE IHREN"
845 PRINT "{down} BEFEHL NICHT." :GOTO 749
860 POKE 214,20 :PRINT :FOR I=1 TO 3 :PRINT EF$:NEXT :PRINT "{up:3}" :RETURN
869 REM **** BEWEGEN ****
870 FOR I=1 TO 6 :IF K$=MID$("NSWOHR",I,1) THEN 880
875 NEXT :GOTO 775
880 IF GS=1 OR ZB=1 OR MR=1 THEN 7600
882 FOR I=1 TO LEN(RI$(P)) STEP 2
885 IF MID$(RI$(P),I,1)=K$THEN 900
890 NEXT :GOSUB 860
895 PRINT "   IN DIESE RICHTUNG GEHT ES NICHT" :GOTO 749
900 SC=0 :ZI=VAL(MID$(ZI$(P),I,2))
905 IF P=16 AND ZI=13 THEN 925
910 IF P=13 AND ZI=9 AND G (8)=0 AND V=1 THEN 7660
915 IF ZI=0 THEN 7680
920 P=ZI :GOTO 180
925 IF G (8)=0 THEN ON V GOTO 930,7650,7670
930 GOSUB 860 :PRINT "{up} HINTER IHNEN LOEST SICH EINE STEIN-"
935 PRINT "{down} LAWINE, DIE DEN WEG VERSCHUETTET." :RI$(16)="W-O" :ZI$(16)="1517"
940 FOR I=1 TO 2500 :NEXT :P=ZI :GOTO 180
949 REM ** SPRUNG ZUR BEF.AUSFUEHRUNG **
950 IF X=4 AND Y<13 THEN 7100
955 IF X=5 THEN 7200
960 ZN=1000+(X-1)*500+(Y-1)*20 :ZN$=STR$(ZN)
965 FOR I=1 TO 4 :NZ=VAL(MID$(ZN$,I+1,1))
970 POKE (2060+I),(NZ+48)
975 NEXT
980 GOSUB 860 :GOTO 1
998 REM *** BEFEHLSAUSFUEHRUNG ***
999 PRINT "  OK !" :GOTO 749
1000 IF Y=1 OR Y=7 OR Y=16 OR Y=19 OR Y=20 THEN E$="E " :GOTO 1002
1001 E$=" "
1002 PRINT " HIER IST KEIN" E$H$(Y) :GOTO 749
1020 IF G (Y)=0 OR G (Y)=P THEN 999
1022 GOTO 1000
1060 GOTO 1020
1180 IF G (10)<>0 AND G (10)<>P THEN 1000
1182 IF V<>2 THEN PRINT " DER " H$(10) " LAESST SICH NICHT OEFFNEN" :GOTO 749
1184 IF G (9)<>21 THEN 999
1186 PRINT " IM " H$(10) " BEFINDET SICH EIN " H$(9) " !!" :G (9)=P :GOTO 749
1300 IF P=18 AND RI$(18)="H" AND V=3 THEN 1306
1302 IF P=1 OR P>13 THEN 1000
1304 PRINT " HIER IST KEINE GESCHLOSSENE TUER" :GOTO 749
1306 IF G (11)<>0 THEN PRINT " DIE TUER IST VERSCHLOSSEN !" :GOTO 749
1308 RI$(18)="O-H" :ZI$(18)="1909" :RI$(19)="S-W" :ZI$(19)="1718" :GOTO 180
1340 IF P<>11 THEN 1000
1342 IF V=3 OR G (10)<>21 THEN 999
1344 IF G (11)<>0 THEN PRINT " DER SCHRANK IST VERSCHLOSSEN" :GOTO 749
1346 PRINT "{up}   IM SCHRANK BEFINDET SICH"
1348 PRINT "{down}   EIN GOLDENER BALL" :G (10)=11 :GOTO 749
1400 PRINT " DAS IST UNMOEGLICH !" :GOTO 749
1580 IF G (5)<>P THEN 1000
1582 IF P=18 THEN PRINT " ES IST ZU DUNKEL HIER !" :GOTO 749
1584 IF G (2)=0 THEN 1590
1586 PRINT "{up} SIE BRAUCHEN EINEN BEHAELTER"
1588 PRINT "{down} UM DEN FROSCH ZU FANGEN !" :GOTO 749
1590 IF GG=5 THEN PRINT " SIE TRAGEN SCHON 5 GEGENSTAENDE !" :GOTO 749
1592 G (5)=0 :GG=GG+1 :PRINT " OK !" :GOTO 749
1740 IF MR=1 THEN 7640
1742 IF V=3 AND P=9 AND MO=0 THEN PRINT " VON HIER AUS GEHT DAS NICHT !" :GOTO 749
1744 GOTO 1000
1780 IF GS<>1 THEN 1000
1782 PRINT "  DER GEIST MAG DAS NICHT !!" :FOR I=1 TO 2000 :NEXT :GOTO 7630
2080 IF G (5)=P THEN PRINT " DER FROSCH LAESST SICH NICHT KUESSEN" :GOTO 749
2082 IF G (5)<>0 THEN 1000
2084 IF V<>1 OR P<>6 THEN 999
2086 PRINT " ES KNALLT UND STINKT..." :FOR I=1 TO 2000 :NEXT :GOTO 7800
2240 IF MR<>1 THEN 1000
2242 PRINT "{up} DAS MONSTER SCHLAEGT WILD UM SICH"
2244 PRINT "{down} UND SCHLEUDERT SIE AUS DEM BRUNNEN" :MR=0
2246 FOR I=1 TO 3000 :NEXT :P=9 :GOTO 180
2260 IF ZB<>1 THEN 1000
2262 FOR I=0 TO 15 :POKE 53281,I :POKE 53280,I :FOR J=1 TO 100 :NEXT :NEXT :ZB=0
2264 POKE 53280,5 :POKE 53281,5 :ZK=ZK+1 :IF ZK=3 THEN 7620
2266 P=2+INT(13*RND(TI)) :GOTO 180
2280 IF GS<>1 THEN 1000
2282 IF TI$<"001000" THEN 7630
2284 GE=1 :GS=0 :FOR I=832 TO 892 STEP 3 :POKE I,0 :POKE I+1,0 :POKE I+2,0 :NEXT
2286 PRINT "{up}  DER GEIST HAT IHREN MUNDGERUCH" :PRINT "{down}  NICHT VERTRAGEN !"
2288 GOTO 749
2400 IF P<>10 OR FA=1 THEN 1000
2402 IF V<>1 THEN 999
2404 IF V=1 THEN FA=1 :RI$(10)="N-W-R" :ZI$(10)="070919"
2406 RI$(19)="S-H" :ZI$(19)="1710" :GOTO 180
2820 IF P<>3 OR LEN(RI$(3))=5 THEN 1000
2822 PRINT " DAS GEHT NICHT !" :GOTO 749
2840 IF P<>11 THEN 1000
2842 PRINT " DER SCHRANK IST ZU SCHWER !" :GOTO 749
2900 IF P<>10 OR FA=1 THEN 1000
2902 GOTO 2822
3560 IF GS=1 THEN 7630
3562 IF G (4)<>0 THEN PRINT " ICH HABE DAS BUCH NICHT !" :GOTO 749
3564 IF V=3 THEN 3568
3566 PRINT " ZAUBERWORT : " Z1$:GOTO 749
3568 PRINT "{up} ZAUBERWOERTER : " Z1$:PRINT "{down}" SPC(17) Z2$:GOTO 749
3660 IF G (9)<>0 THEN PRINT " ICH HABE KEINEN ZETTEL" :GOTO 749
3662 PRINT " ZAUBERWORT: " Z2$:FOR I=1 TO 2500 :NEXT
3664 PRINT "{up} DER ZETTEL VERSCHWINDET !!!" :GG=GG-1 :G (9)=21 :GOTO 749
3860 IF P<>12 OR SC=0 THEN 1000
3862 PRINT "{up}  TUT MIR LEID, ABER ES SCHEINT"
3864 PRINT "{down}  EINE FREMDE SPRACHE ZU SEIN" :GOTO 749
4300 IF P<12 OR (V=3 AND P=18) THEN PRINT " OK...NICHTS PASSIERT !" :GOTO 749
4302 GOTO 1000
4320 IF P<>3 OR LEN(RI$(3))=5 THEN 1000
4322 PRINT "{up} HINTER DEM " H$(17)
4324 PRINT "{down} SCHEINT EIN HOHLRAUM ZU SEIN !" :GOTO 749
4380 IF V=2 AND P=12 AND LEN(RI$(12))=3 THEN 4388
4382 IF P=19 AND G (8)=21 THEN 4392
4384 IF P>14 AND P<18 THEN 1000
4386 PRINT " OK...NICHTS BESONDERES FESTZUSTELLEN !" :GOTO 749
4388 POKE 214,11 :PRINT :PRINT SPC(18) "{yellow}{rvrs on}U{$ab}/W" :GOSUB 860
4390 PRINT " EINE SCHRIFT ERSCHEINT..." :SC=1 :GOTO 749
4392 G (8)=19 :PRINT " AN DER WAND HAENGT EIN SCHWERT !" :GOTO 749
4400 IF P=10 AND FA=0 THEN PRINT " KLINGT HOHL..." :GOTO 749
4402 GOTO 1000
4580 IF G (5)<>0 AND G (5)<>P THEN 1000
4582 PRINT " DER FROSCH ENTWISCHT IHNEN !"
4584 IF G (5)<>18 OR V<>1 THEN 4588
4586 GOTO 749
4588 IF G (5)=0 THEN GG=GG-1
4590 G (5)=1+INT(19*RND(1)) :GOTO 749
4740 IF MR=0 THEN 1000
4742 PRINT " GUTE IDEE, ABER DAS GEHT NICHT !" :GOTO 749
4760 IF ZB=0 THEN 1000
4762 GOSUB 4770 :IF Y=0 OR G (Y)<>0 THEN 749
4764 IF Y=8 AND ZA=2 THEN 7820
4766 GOTO 7620
4770 INPUT " WOMIT ";WM$
4771 FOR I=1 TO LEN(WM$) :IF MID$(WM$,I,1)=" " THEN 4773
4772 NEXT :GOTO 4774
4773 WM$=RIGHT$(WM$,LEN(WM$)-I)
4774 FOR Y=1 TO 12 :IF WM$=LEFT$(H$(Y),LEN(WM$)) THEN 4776
4775 NEXT :GOSUB 860 :PRINT " ICH VERSTEHE '" WM$"' NICHT !" :Y=0 :RETURN
4776 IF G (Y)<>0 THEN GOSUB 860 :PRINT " ICH HABE DIESEN GEGENSTAND NICHT !"
4777 RETURN
4780 IF GS=0 THEN 1000
4782 GOSUB 4770 :IF Y=0 OR G (Y)<>0 THEN 749
4784 GOSUB 860 :ON V GOTO 4785,4790,4794
4785 IF G (8)=0 THEN PRINT " SIE SIND GESTOLPERT, UND..." :FOR I=1 TO 2000 :NEXT :GOTO 7630
4786 IF ZA=1 AND Y=7 THEN 4798
4788 GOTO 7630
4790 IF ZA=1 AND Y=3 THEN 4798
4792 GOTO 7630
4794 IF Y=1 THEN PRINT " DER GEIST IST TOT, ABER..." :FOR I=1 TO 2000 :NEXT
4796 GOTO 7630
4798 POKE VI+21,0 :GS=0 :GE=1 :PRINT " OK...DER GEIST IST TOT !" :GOTO 749
5000 IF G (1)<>0 AND G (1)<>P THEN 1000
5002 PRINT " WOZU SOLLTE DAS GUT SEIN ?" :GOTO 749
5020 IF G (2)<>0 AND G (2)<>P THEN 1000
5022 PRINT "{up} SO GEHT MAN NICHT MIT FREMDEM"
5024 PRINT "{down} EIGENTUM UM !" :GOTO 749
5300 IF P=18 AND RI$(18)="H" AND V=3 THEN 5306
5302 IF P=1 OR P>13 THEN 1000
5304 PRINT "WIESO, HIER IST KEINE VERSCHLOSSENE TUER" :GOTO 749
5306 PRINT " DIE TUER IST ZU STABIL..." :GOTO 749
5320 GOSUB 4770 :IF Y=0 OR G (Y)<>0 THEN 749
5322 IF (V=1 AND Y=8) OR (V=3 AND ZA=2 AND Y=8) THEN 7247
5324 GOSUB 860 :PRINT " DAS GEHT NICHT !" :GOTO 749
5340 IF P<>11 THEN 1000
5342 PRINT "{up} DER SCHRANK LAESST SICH NICHT"
5344 PRINT "{down} MIT GEWALT OEFFNEN !" :GOTO 749
5380 IF P>14 AND P<18 THEN 1000
5382 PRINT "SIE KOENNEN DIE WAND NICHT ZERSTOEREN !" :GOTO 749
6499 REM *** BEFEHL 'SAGE' ***
6500 GOSUB 860 :IF H$=Z1$ THEN ZA=1 :GOTO 6506
6502 IF H$=Z2$ THEN ZA=2 :GOTO 6506
6504 GOTO 7690
6506 ON V GOTO 6508,6512,6520
6508 IF ZA=1 AND G (7)=0 AND GS=1 AND G (4)=0 THEN PRINT " DIE KEULE VIBRIERT..." :GOTO 6528
6510 GOTO 6526
6512 IF ZA=1 AND P=12 AND SC=1 THEN RI$(12)="N-W-O" :ZI$(12)="080013" :GOTO 415
6514 IF ZA=1 AND G (3)=0 AND GS=1 THEN POKE VI+39,0 :GOTO 755
6516 IF ZA=2 AND G (8)=0 AND ZB=1 AND G (4)=0 THEN PRINT " DAS SCHWERT LEUCHTET..." :GOTO 6528
6518 GOTO 6526
6520 IF ZA=1 AND G (3)=0 AND MO=0 AND P=9 THEN PRINT " DAS AMULETT LEUCHTET..." :GOTO 6528
6522 IF ZA<>2 OR G (4)<>0 OR P<>3 OR RI$(3)<>"W-O" THEN 6526
6524 PRINT " DER SPIEGEL LEUCHTET..." :GOTO 6528
6526 PRINT " OK...ES TUT SICH NICHTS !" :GOTO 749
6528 FOR I=1 TO 2000 :NEXT :GOTO 755
6999 REM * BESITZ/GEGENSTD ANZEIGEN *
7000 PRINT "{home}";:FOR I=1 TO 20 :PRINT "        " :NEXT
7002 PRINT "{home}{black}{down}SIE" :PRINT "{down}BESITZEN" :SP=0
7005 FOR I=1 TO 12 :IF G (I)=0 THEN GOSUB 7050
7010 NEXT
7020 PRINT "{home}";:FOR I=1 TO 20 :PRINT SPC(32) "       " :NEXT
7022 J=0 :PRINT "{home}{black}{down}" SPC(32) "ES GIBT" :PRINT SPC(32) "{down}HIER :" :SP=32
7025 FOR I=1 TO 12 :IF G (I)=P THEN GOSUB 7050 :J=J+1 :IF J=6 THEN RETURN
7030 NEXT :RETURN
7050 IF I=1 THEN PRINT SPC(SP) "{down}GLAS-" :PRINT SPC(SP) "KUGEL" :RETURN
7055 IF I=11 THEN PRINT SPC(SP) "{down}SCHLUES" :PRINT SPC(SP) "SEL" :RETURN
7060 IF I=12 THEN PRINT SPC(SP) "{down}KETTEN-" :PRINT SPC(SP) "HEMD" :RETURN
7065 PRINT SPC(SP) "{down}" G$(I) :RETURN
7099 REM *** BEFEHL 'NIMM' ***
7100 IF G (Y)=P THEN 7110
7105 GOSUB 860 :PRINT "  DIESER GEGENSTAND IST NICHT HIER !" :GOTO 749
7110 IF GG<5 THEN 7120
7115 GOSUB 860 :PRINT "  SIE HABEN BEREITS 5 GEGENSTAENDE !" :GOTO 749
7120 IF Y=5 THEN GOSUB 860 :PRINT "    DER FROSCH HUEPFT IHNEN DAVON !" :GOTO 749
7125 G (Y)=0 :GG=GG+1 :GOSUB 860 :PRINT "   OK !" :FOR I=1 TO 1000 :NEXT :GOTO 750
7199 REM *** BEFEHL 'WIRF' ***
7200 IF G (Y)<>0 THEN GOSUB 860 :PRINT " ICH HABE DIESEN GEGENSTAND NICHT !" :GOTO 749
7205 GOSUB 860 :INPUT "  WOHIN ";WH$
7210 FOR I=1 TO LEN(WH$) :IF MID$(WH$,I,1)=" " THEN 7220
7215 NEXT :GOTO 7225
7220 WH$=RIGHT$(WH$,LEN(WH$)-I)
7225 IF WH$="BODEN" OR WH$="WEG" OR WH$="EGAL" OR WH$="WAND" THEN 7235
7230 ON V GOTO 7240,7270,7330
7235 GOSUB 860 :PRINT "  OK !" :G (Y)=P :GG=GG-1 :GOTO 749
7240 IF Y<>8 OR WH$<>"SPIEGEL" OR P<>3 OR LEN(RI$(3))=5 THEN 7255
7245 G (Y)=3 :GG=GG-1
7247 RI$(3)="N-W-O" :ZI$(3)="010204" :GOSUB 860
7250 PRINT "  DER SPIEGEL ZERBRICHT !" :FOR I=1 TO 2000 :NEXT :GOTO 180
7255 IF Y<>10 OR WH$<>"BRUNNEN" OR P<>9 OR G (5)<>18 THEN 7380
7260 GOSUB 860 :PRINT "{up}  AUS DEM BRUNNEN KOMMT EIN FROSCH"
7265 PRINT "{down}  MIT DEM BALL IM MUND." :G (10)=9 :G (5)=9 :GG=GG-1 :GOTO 749
7270 IF Y<>3 OR WH$<>"GEIST" OR GS<>1 THEN 7290
7275 IF ZA<>1 THEN 7630
7280 POKE VI+21,0 :GOSUB 860 :PRINT "    SIE HABEN DEN GEIST BESIEGT !"
7285 GE=1 :GS=0 :G (Y)=7 :GG=GG-1 :GOTO 749
7290 IF Y<>6 OR P<>10 OR WH$<>"QUADRAT" OR FA=1 THEN 7305
7295 FA=1 :G (6)=10 :GG=GG-1 :RI$(10)="N-W-R" :ZI$(10)="070919"
7300 RI$(19)="S-H" :ZI$(19)="1710" :GOTO 180
7305 IF Y<>4 OR P<>3 OR WH$<>"SPIEGEL" OR LEN(RI$(3))=5 THEN 7315
7310 GOTO 7245
7315 IF Y<>8 OR ZB<>1 OR WH$<>"ZAUBERER" THEN 7380
7320 IF ZA<>2 THEN 7620
7325 POKE VI+21,0 :FOR I=1 TO 2000 :NEXT :GOTO 7820
7330 IF Y<>1 OR WH$<>"GEIST" OR GS<>1 THEN 7340
7335 GOTO 7280
7340 IF Y<>3 OR WH$<>"BRUNNEN" OR P<>9 THEN 7360
7345 G(3)=18 :GG=GG-1 :GOSUB 860 :IF MO=1 THEN PRINT "  OK !" :GOTO 749
7350 IF ZA=1 THEN PRINT " AUS DEM BRUNNEN ERTOENT EIN SCHREI..." :MO=1 :GOTO 749
7355 G(3)=9 :PRINT " DAS AMULETT KOMMT ZURUECKGEFLOGEN !" :GOTO 749
7360 IF Y<>8 OR WH$<>"SPIEGEL" OR P<>3 OR LEN(RI$(3))=5 THEN 7380
7365 IF ZA=2 THEN 7245
7370 GOTO 7235
7380 IF WH$="BRUNNEN" AND P=9 THEN G(Y)=18 :GOTO 7415
7382 IF WH$="QUADRAT" AND P=10 AND FA=0 THEN G(Y)=10 :GOTO 7415
7385 IF WH$="HOEHLE" AND P=17 THEN G(Y)=19 :GOTO 7415
7390 IF WH$="GEIST" AND GS=1 THEN 7630
7395 IF WH$="ZAUBERER" AND ZB=1 THEN 7620
7400 IF WH$="SPIEGEL" AND P=3 AND LEN(RI$(3))=3 THEN 7235
7405 GOSUB 860 :PRINT " ICH VERSTEHE SIE NICHT..."
7410 FOR I=1 TO 2000 :NEXT :GOTO 7205
7415 GG=GG-1 :GOSUB 860 :PRINT "  OK !" :GOTO 749
7499 REM *** ERKLAERUNG ***
7500 PRINT "{clear}{down} SIE MUESSEN VERSUCHEN, EINE AUFGABE ZU"
7505 PRINT "{down} LOESEN, INDEM SIE DEM COMPUTER SAGEN,"
7510 PRINT "{down} WAS ER TUN SOLL."
7515 PRINT "{down} ZUR  FORTBEWEGUNG  GEBEN  SIE NUR"
7520 PRINT "{down} ABKUERZUNGEN EIN (N-S-W-O-H-R = NORD -" :REM N-S-W-O-H-R = NORD -
7525 PRINT "{down} SUED - WEST - OST - HOCH - RUNTER)"
7526 REM SUED - WEST - OST - HOCH - RUNTER
7530 PRINT "{down} ANSONSTEN MUESSEN DIE BEFEHLE AUS ZWEI"
7535 PRINT "{down} WOERTERN BESTEHEN (VERB/HAUPTWORT)"
7540 PRINT "{down} DER COMPUTER VERSTEHT DIE VERBEN :"
7545 PRINT "{down} OEFFNE,FANGE,KUESSE,NIMM,LIES,WIRF,"
7550 PRINT "{down} BERUEHRE/KLOPFE,ERSCHLAGE/TOETE,SAGE"
7555 PRINT "{down} ZERBRECHE/ZERSTOERE   -  FERTIG ???"
7560 POKE 198,0 :WAIT 198,1 :GOTO 100
7599 REM **** VERLOREN ****
7600 IF ZB=1 THEN 7620
7605 IF GS=1 THEN 7630
7610 IF MR=1 THEN 7640
7620 PRINT "{clear}{black}{down} DER ZAUBERER HAT SIE IN EINE KROETE"
7625 PRINT "{down} VERWANDELT." :GOTO 7700
7630 PRINT "{clear}{black}{down}     DER GEIST HAT SIE GEBISSEN." :GOTO 7700
7640 PRINT "{clear}{black}{down} DAS MONSTER HAT BESCHLOSSEN, SIE"
7645 PRINT "{down} ZU HEIRATEN." :GOTO 7700
7650 PRINT "{clear}{black}{down} SIE SIND UNTERWEGS GESTOLPERT UND"
7655 PRINT "{down} HABEN SICH DABEI DEN KLEINEN FINGER" :PRINT "{down} VERSTAUCHT." :GOTO 7700
7660 PRINT "{clear}{black}{down} AUS DEM TORBOGEN HAT SICH EIN STEIN"
7665 PRINT "{down} GELOEST UND IST IHNEN AUF DEN FUSS" :PRINT "{down} GEFALLEN." :GOTO 7700
7670 PRINT "{clear}{black}{down} SIE SIND IN EINEN ERDRUTSCH GERATEN"
7675 PRINT "{down} UND HABEN SICH IHRE NEUE HOSE DRECKIG" :PRINT "{down} GEMACHT." :GOTO 7700
7680 PRINT "{clear}{black}{down} SIE SIND VOM BERG GEFALLEN UND HABEN"
7685 PRINT "{down} DABEI IHRE BRILLE ZERBROCHEN." :GOTO 7700
7690 PRINT "{clear}{black}{down}        SIE HABEN SICH IN EINE"
7695 PRINT "{down}          BANANE VERWANDELT !"
7700 PRINT "{down} DESHALB MUESSEN SIE LEIDER AUFGEBEN."
7705 PRINT "{down:3}{yellow} MOECHTEN SIE ES NOCH EINMAL PROBIEREN ?"
7710 GET I$:IF I$="J" THEN RUN 5
7715 IF I$<>"N" THEN 7710
7720 END
7799 REM **** GEWONNEN ****
7800 PRINT "{clear}{yellow}{down}        ... UND  DER  " H$(5) " ..." :FOR I=1 TO 1500 :NEXT
7805 PRINT "{up}         ... VERWANDELT  SICH ..." :FOR I=1 TO 1500 :NEXT
7810 PRINT "{up}... IN EINE WUNDERSCHOENE PRINZESSIN ..." :FOR I=1 TO 2000 :NEXT
7815 PRINT "{clear}{down:5}" :GOTO 7840
7820 POKE VI+21,0 :PRINT "{clear}{yellow}{down:6}    SIE HABEN DEN ZAUBERER BESIEGT !!!" :GOTO 7840
7830 PRINT "{clear}{yellow}{down:6}    SIE HABEN DEN SCHATZ GEFUNDEN !!!"
7840 PRINT SPC(13) "{down:2}GRATULIERE," :PRINT "{down}     SIE HABEN IHR ZIEL ERREICHT !!"
7850 PRINT "{down:3}      WOLLEN SIE WEITER SPIELEN ?"
7860 GET I$:IF I$="J" THEN RUN 1
7870 IF I$<>"N" THEN 7860
7880 END
8000 DATA OEFFNE,BDJPRU,FANGE,EMO,KUESSE,EMNOU,NIMM,ABCDEFGHIJKLQRU
8005 DATA WIRF,ABCDEFGHIJKL,LIES,DIS,BERUEHRE,PQTU,"TOETE",EMNO
8010 DATA ZERSTOERE,ABPQRT,WERFE,LESE,KLOPFE,ERSCHLAGE,ZERBRECHE
8015 DATA GLASKUGEL,KAEFIG,AMULETT,BUCH,FROSCH,HELM,KEULE,SCHWERT,ZETTEL,BALL
8020 DATA SCHLUESSEL,KETTENHEMD,MONSTER,ZAUBERER,GEIST,TUER,SPIEGEL,SCHRANK
8025 DATA SCHRIFT,WAND,QUADRAT
8030 DATA S,03,"S-O",0503,"W-O",0204,"W-H",0311,"N-S",0208,O,07,"S-W",1006,"N-O",0509
8035 DATA "S-W-O-R",13081018,"N-W",0709,R,04,"W-O",0013,"N-W-O",091214,"W-O",1300,O,16
8040 DATA "W-O-H",151713,"N-W",1916,H,09,S,17
8045 DATA 20,20,20,20,18,2,21,21,21,21,6,5
8050 DATA SILBER,BLUME,GOLD,RING,KUPFER,HORN,PLATIN,STERN
8054 REM SPRITE 1 - GEIST
8055 DATA 0,127,0,0,255,192,1,206,96,3,255,240,1,255,240,1,245,248,1,255,248
8060 DATA 3,255,248,7,255,248,15,255,252,31,255,254,3,255,240,3,255,224
8065 DATA 3,255,224,7,255,224,7,255,224,7,255,240,15,255,240,15,255,248
8070 DATA 31,255,252,59,187,110
8074 REM SPRITE 2 - MONSTER
8075 DATA 12,0,48,30,0,120,3,0,192,1,129,128,3,195,192,15,231,240,62,126,124
8080 DATA 124,60,62,248,60,31,254,126,127,255,255,255,127,255,254,127,255,254
8085 DATA 63,90,252,30,165,120,31,255,248,7,255,224,4,126,32,4,60,32
8090 DATA 31,0,248,42,129,84
8094 REM SPRITE 3 - ZAUBERER
8095 DATA 0,15,224,0,63,128,0,127,128,0,219,0,1,255,0,3,255,128,31,255,240
8100 DATA 127,255,252,127,255,252,111,255,236,111,255,236,103,255,204,98,170,140
8105 DATA 99,255,140,71,255,196,7,199,192,15,131,224,15,1,224,14,0,224
8110 DATA 28,0,112,60,0,120
8200 REM
8201 REM **********************
8202 REM *   CASTLE OF DOOM   *
8203 REM * VON B. WEISSBECKER *
8204 REM *  MAERZ/APRIL 1984  *
8205 REM **********************
